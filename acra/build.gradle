apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion Integer.parseInt(androidVersion)

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion androidVersion
        versionName version
        consumerProguardFile proguardFile
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

dependencies {
    implementation "com.android.support:support-compat:$supportVersion"
    implementation "com.android.support:support-fragment:$supportVersion"
    implementation "com.android.support:support-annotations:$supportVersion"
    annotationProcessor project(':annotationprocessor')
    compileOnly project(':annotations')
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs + files('build/generated/source/buildConfig/release') + files('build/generated/ap_generated_sources/release/out')
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
    afterEvaluate {
        dependsOn assembleRelease
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}


afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                artifact androidJavadocsJar
                artifact androidSourcesJar

                groupId = project.group
                artifactId = project.archivesBaseName
                version = project.version

                pom {
                    name = 'Application Crash Report for Android'
                    description = 'Publishes a report of an Android application crash to an end point.'
                    packaging = 'aar'
                    url = 'http://acra.ch'

                    scm {
                        connection = 'scm:git:https://github.com/ACRA/acra.git'
                        developerConnection = 'scm:git:git@github.com:ACRA/acra.git'
                        url = 'https://github.com/ACRA/acra.git'
                    }

                    licenses {
                        license {
                            name = 'Apache 2'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'kevin.gaudin'
                            name = 'Kevin Gaudin'
                        }
                        developer {
                            id = 'william.ferguson'
                            name = 'William Ferguson'
                        }
                        developer {
                            id = 'f43nd1r'
                            name = 'Lukas Morawietz'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "MavenCentral"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username = ossrhUser
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}
